#version 330

#include gBufferUtil.glsl

layout (location = 0) out vec4 out_Colour;

uniform sampler2D gDepth;
uniform sampler2D gNormal;
uniform sampler2D gAlbedo;
uniform sampler2D gMaterials;

uniform vec3 lightPositionViewSpace;
uniform vec3 lightDirectionViewSpace;
uniform vec3 lightColor;
uniform vec3 attenuation;
uniform float softness;

uniform vec2 resolution;
uniform mat4 invProjection;


const float PI = 3.14159265359;
float DistributionGGX(vec3 N, vec3 H, float roughness);
float GeometrySchlickGGX(float NdotV, float roughness);
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness);
vec3 fresnelSchlick(float cosTheta, vec3 F0);


void main(void){
    vec2 textureCoords = gl_FragCoord.xy / resolution;
	vec3 position = ReconstructViewSpacePosition(gl_FragCoord.xy, texture(gDepth, textureCoords).r, invProjection, resolution);
	vec3 normal = texture(gNormal, textureCoords).xyz;
	vec3 albedo = texture(gAlbedo, textureCoords).rgb;
	

	float ambientOcclusion = texture(gNormal, textureCoords).a;
	float roughness = texture(gMaterials, textureCoords).r;
	float emission = texture(gMaterials, textureCoords).g;
	float metallic = texture(gMaterials, textureCoords).b;
    
    vec3 lightDir = normalize(lightPositionViewSpace - position);  
    float theta = dot(lightDir, normalize(-lightDirectionViewSpace));
    float cutoff = 3.1415f/4;
    if (theta < cutoff) 
    {       
      discard;
    }
    
    float epsilon = softness*cutoff;
    float intensity = smoothstep(0, 1, clamp((theta - cutoff) / epsilon, 0.0f, 1.0f));  

	vec3 viewDir = normalize(-position);

	vec3 F0 = vec3(0.04); 
	vec3 Lo = vec3(0.0);
    F0 = mix(F0, albedo, metallic);
	vec3 N = normalize(normal);
    vec3 V = viewDir;

    // calculate per-light radiance
    vec3 L = normalize(lightPositionViewSpace - position);
    vec3 H = normalize(V + L);
    float distance = length(lightPositionViewSpace - position.xyz);
	float attenuationFactor = clamp(1.0 / (attenuation.x + attenuation.y * distance+ attenuation.z * distance * distance)-1, 0, 999);
    vec3 radiance     = lightColor*attenuationFactor;        
        
    // cook-torrance brdf
    float NDF = DistributionGGX(N, H, roughness);        
    float G   = GeometrySmith(N, V, L, roughness);    
	
    vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);       
        
    vec3 kS = F;
    vec3 kD = vec3(1.0) - kS;
    kD *= 1.0 - metallic;	  
    vec3 numerator    = NDF * G * F;
    float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.0001;
    vec3 specular     = numerator / denominator;  
            
    // add to outgoing radiance Lo
    float NdotL = max(dot(N, L), 0.0);                
    Lo += (kD * albedo / PI + specular) * radiance * NdotL; 

	//vec3 ambient = vec3(0.03) * albedo * ambientOcclusion;
    vec3 color =  Lo+ambientOcclusion*albedo*0.001f*radiance;
    color *= intensity;

	//color = color / (color + vec3(1.0));
    //color = pow(color, vec3(1.0/2.2));  
	//lighting = applyFog(lighting, -position.z, -viewDir);
	out_Colour = vec4(color, 1.0);
	//out_Colour =  vec4(1f, 0f, 10f, 1.0f);
	//out_Colour =  vec4(positionSunSpace.xyz, 1.0f);
	//out_Colour =  vec4(vec3(emission), 1.0f);

	
}

float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a      = roughness*roughness;
    float a2     = a*a;
    float NdotH  = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;
	
    float num   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;
	
    return num / denom;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float num   = NdotV;
    float denom = NdotV * (1.0 - k) + k;
	
    return num / denom;
}
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2  = GeometrySchlickGGX(NdotV, roughness);
    float ggx1  = GeometrySchlickGGX(NdotL, roughness);
	
    return ggx1 * ggx2;
}
vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}  